/*
 * A library part of hale-core.
 */

plugins {
  id 'java'
  id 'maven-publish'

  id 'com.diffplug.spotless'

  id 'io.qameta.allure-adapter'

  id 'hale.common'
}

spotless {
  java {
    importOrder('java', 'javax', 'org', 'com', '')

    removeUnusedImports()

    // https://github.com/diffplug/spotless/tree/main/plugin-gradle#eclipse-jdt
    eclipse().configFile(rootProject.file('eclipse-format.xml'))

    if (HaleBuild.useDefaultHeader(project)) {
      licenseHeader(HaleBuild.LICENSE_HEADER_DEFAULT)
    }

    trimTrailingWhitespace()
    endWithNewline()
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

dependencies {
  testRuntimeOnly libs.logback.core
  testRuntimeOnly libs.logback.classic
  testRuntimeOnly project(':common:plugins:eu.esdihumboldt.hale.common.logback.config.test')

  // make offline resources available to all tests
  testRuntimeOnly project(':util:features:eu.esdihumboldt.util.feature.resource')
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

/*
task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}
*/

/*
 * Publishing to Maven
 *
 * https://docs.gradle.org/current/userguide/publishing_maven.html
 */
publishing {
  publications {
    library(MavenPublication) {
      from components.java
      artifact sourcesJar
      // artifact javadocJar
    }
  }

  repositories {
    maven {
      url 'https://artifactory.wetransform.to/artifactory/' +
        (version.endsWith('-SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local')
      credentials {
        username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
        password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
      }
    }
  }
}

/*
 * Allure configuration
 *
 * See https://allurereport.org/docs/junit4-configuration/
 */

task generateAllureProperties {
  doLast {
    // Get the first directory of the test resources source set
    def outputDir = sourceSets.test.resources.srcDirs.iterator().next()

    // Specify the name of the generated file
    def outputFile = new File(outputDir, "allure.properties")

    // Create the directory if it doesn't exist
    outputDir.mkdirs()

    def properties = [:]

    // defaults
    properties['allure.link.issue.pattern'] = 'https://github.com/halestudio/hale-core/issues/{}'
    properties['allure.link.JIRA.pattern'] = 'https://wetransform.atlassian.net/browse/{}'

    def config = project.hale.allure as AllureConfig

    // derive default epic from project structure
    def p = project.parent?.parent
    if (p != null) {
      switch (p.name) {
        case 'io':
          properties.epic = 'Import and export formats'
          break
        case 'util':
          properties.epic = 'Utilities'
          break
        case 'cst':
          properties.epic = 'Conceptual Schema Transformer'
          break
        case 'ext':
          properties.epic = 'Extensions'
          break
        case 'common':
          properties.epic = 'Common libraries'
          break
        case 'app':
          properties.epic = 'Command line applications'
          break
      }
    }

    def labels = ['epic', 'story', 'feature']
    labels.each {label ->
      if (config."$label") {
        properties[label] = config."$label"
      }
    }

    // Write some content to the file
    outputFile.text = properties.collect { "${it.key} = ${it.value}" }.join('\n')
  }
}

processTestResources.dependsOn generateAllureProperties
