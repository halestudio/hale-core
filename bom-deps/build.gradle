plugins {
  id 'hale.bom-conventions'
}

description = 'hale-core dependencies BOM'

javaPlatform {
  allowDependencies() // for including other BOMs
}

dependencies {
  constraints {
    // include all third party dependencies from version catalog
    def versionCatalog = rootProject.extensions.getByType(VersionCatalogsExtension).named("libs")
    versionCatalog.libraryAliases.each { alias ->
      api(versionCatalog.findLibrary(alias).get())
    }

    // update transitive dependencies manually if needed, e.g. to fix CVE issues - alternatively add to version catalog (where it is picked up from above)

    api("org.lz4:lz4-java:1.8.1") // update to fix CVE-2025-12183
  }

  /*
   * Other BOMs
   */

  // Groovy BOM
  api(platform("org.codehaus.groovy:groovy-bom:${libs.versions.groovy.get()}"))

  // SLF4J BOM
  api(platform("org.slf4j:slf4j-bom:${libs.versions.slf4j.get()}"))
}
