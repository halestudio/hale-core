plugins {
  id 'hale.bom-conventions'
}

description = 'hale-core BOM'

javaPlatform {
  allowDependencies() // for including other BOMs
}

dependencies {
  // include all repository projects
  constraints {
    // Note: transitive dependencies are not included for a dependency

    rootProject.subprojects.each { p ->
      // add all projects except:
      // - root project
      // - this project
      // - allure project
      // - intermediate projects w/o build.gradle
      if (p != project && p.name != 'allure' && new File(p.projectDir, 'build.gradle').exists()) {
        api(p)
      }
    }
  }

  /*
   * Other BOMs
   */

  // Groovy BOM
  api(platform("org.codehaus.groovy:groovy-bom:${libs.versions.groovy.get()}"))
}
